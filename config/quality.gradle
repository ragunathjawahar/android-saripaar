apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

check.dependsOn 'checkstyle', 'findbugs', 'pmd'

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/quality/checkstyle/checkstyle.xml")
    source 'src'
    include '**/com/mobsandgeeks/saripaar/*.java'
    exclude '**/gen/**'

    classpath = files()
}

task findbugs(type: FindBugs) {
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
    excludeFilter = new File("${project.rootDir}/config/quality/findbugs/findbugs-filter.xml")
    classes = files("$project.buildDir/intermediates/classes/")

    source 'src'
    include '**/com/mobsandgeeks/saripaar/*.java'
    exclude '**/gen/**'

    reports {
        xml {
            destination file("$project.buildDir/reports/findbugs/findbugs.xml")
            xml.withMessages true
        }
    }

    classpath = files()
}

task pmd(type: Pmd) {
    ruleSetFiles = files("${project.rootDir}/config/quality/pmd/pmd-ruleset.xml")
    ignoreFailures = true
    ruleSets = ["java-basic", "java-braces", "java-strings"]

    source 'src'
    include '**/com/mobsandgeeks/saripaar/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        html.enabled = true
    }
}
